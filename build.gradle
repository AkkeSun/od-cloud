plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.epages.restdocs-api-spec' version '0.19.0'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // FCM
    implementation 'com.google.firebase:firebase-admin:9.2.0'

    // ShedLock
    implementation 'net.javacrumbs.shedlock:shedlock-spring:5.10.2'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.10.2'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // swagger
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // springboot 3 jwt
    implementation "io.jsonwebtoken:jjwt:0.9.1"
    implementation "javax.xml.bind:jaxb-api:2.3.0"

    implementation 'org.apache.tika:tika-core:1.27'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'mysql:mysql-connector-java:8.0.33'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'com.github.codemonstur:embedded-redis:1.4.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

sourceSets {
    main {
        resources {
            srcDirs "src/main/deployment/${profile}"
        }
    }
}

test {
    useJUnitPlatform()
}


swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

// ./gradlew openapi3 copyOasToSwagger
openapi3 {
    servers = [
            { url = "https://cloud.odlab.kr" }
    ]
    title = "Cloud API"
    description = "모이미즘 API 입니다."
    version = "0.0.1"
    format = "yaml"
    outputDirectory = "src/main/resources/static/docs"
    outputFileNamePrefix = 'od-cloud-api'
}

tasks.register('copyOasToSwagger', DefaultTask) {
    doLast {
        def openapi3File = file("src/main/resources/static/docs/od-cloud-api.yaml")
        def securitySchemesContent = "  securitySchemes:\n" +                                                              \
                                                                                                  "    APIKey:\n" +                                                              \
                                                                                                  "      type: apiKey\n" +                                                              \
                                                                                                  "      name: Authorization\n" +                                                              \
                                                                                                  "      in: header\n" +                                                             \
                                                                                                  "security:\n" +
                "  - APIKey: []"

        if (openapi3File.exists()) {
            def existingContent = openapi3File.text
            def updatedContent = existingContent + "\n" + securitySchemesContent
            openapi3File.text = updatedContent
            println "Added JWT authentication to ${openapi3File.path}"
        } else {
            println "Warning: openapi3.yaml not found at ${openapi3File.path}"
        }
    }
}

bootJar {
    archiveFileName = "od-cloud.jar"
}

jar {
    enabled = false
}


